apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "es.gob.radarcovid"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 4
        versionName "1.0"

        testInstrumentationRunner "androidx.background_shape_exposition_low.runner.AndroidJUnitRunner"

        resConfigs "es"
    }

    signingConfigs {
        releaseOld {
            //THIS KEY WAS THE KEY USED WITH THE OLD PACKAGE NAME (es.gob.covidradar)
            storeFile file("../app.keystore")
            storePassword "es.gob"
            keyAlias "covid19"
            keyPassword "covid19"
        }
        release {
            storeFile file("../app.keystore")
            storePassword "es.gob"
            keyAlias "radarcovid"
            keyPassword "radarcovid"
        }
    }

    buildTypes {
        mock {
            debuggable true
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules-debug.pro'
            signingConfig signingConfigs.release

            buildConfigField 'Boolean', 'isMock', 'true'
        }
        debug {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules-debug.pro'

            buildConfigField 'Boolean', 'isMock', 'false'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release

            buildConfigField 'Boolean', 'isMock', 'false'

            packagingOptions {
                exclude 'DebugProbesKt.bin'
            }
        }

    }

    flavorDimensions 'environment'
    productFlavors {
        pre {
            dimension = 'environment'
            buildConfigField "String", 'API_URL', '"https://d3d0clxg4q92wk.cloudfront.net/"'
            buildConfigField "String", 'CONTENTFUL_URL', '"https://cdn.contentful.com/spaces/fkltdlmuiyh6/environments/master/"'

            buildConfigField 'String', 'CONFIG_URL', '"https://d3d0clxg4q92wk.cloudfront.net/dp3t/"'
            buildConfigField 'String', 'BUCKET_URL', '"https://d3d0clxg4q92wk.cloudfront.net/dp3t/"'
            buildConfigField 'String', 'REPORT_URL', '"https://d3d0clxg4q92wk.cloudfront.net/dp3t/"'
            buildConfigField 'String', 'PUBLIC_KEY', '"LS0tLS1CRUdJTiBQVUJMSUMgS0VZLS0tLS0KTUZrd0V3WUhLb1pJemowQ0FRWUlLb1pJemowREFRY0RRZ0FFdmx1bzYyTFVVcFllcVVGM3haWVhYSG03cjBGWApScENFbVBqTUlxUHVERjcvYmRua1FIbndxbVNoVzIvOU9BcllEd09FUUZmdEE4ZDV6T3NEZmh0T2NRPT0KLS0tLS1FTkQgUFVCTElDIEtFWS0tLS0t"'
        }
        pro {
            dimension = 'environment'
            buildConfigField "String", 'API_URL', '"https://radarcovid.covid19.gob.es/"'
            buildConfigField "String", 'CONTENTFUL_URL', '"https://cdn.contentful.com/spaces/fkltdlmuiyh6/environments/master/"'

            buildConfigField 'String', 'CONFIG_URL', '"https://radarcovid.covid19.gob.es/dp3t/"'
            buildConfigField 'String', 'BUCKET_URL', '"https://radarcovid.covid19.gob.es/dp3t/"'
            buildConfigField 'String', 'REPORT_URL', '"https://radarcovid.covid19.gob.es/dp3t/"'
            buildConfigField 'String', 'PUBLIC_KEY', '"LS0tLS1CRUdJTiBQVUJMSUMgS0VZLS0tLS0KTUZrd0V3WUhLb1pJemowQ0FRWUlLb1pJemowREFRY0RRZ0FFdmx1bzYyTFVVcFllcVVGM3haWVhYSG03cjBGWApScENFbVBqTUlxUHVERjcvYmRua1FIbndxbVNoVzIvOU9BcllEd09FUUZmdEE4ZDV6T3NEZmh0T2NRPT0KLS0tLS1FTkQgUFVCTElDIEtFWS0tLS0t"'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    // The google exposure library, required for DP3T, is resolved by the aar located in libs folder

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    // Recommended: Add the Firebase SDK for Google Analytics.
    implementation 'com.google.firebase:firebase-analytics-ktx:17.4.4'

    // Add the Firebase Crashlytics SDK.
    implementation 'com.google.firebase:firebase-crashlytics:17.1.1'

    //Views
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.viewpager2:viewpager2:1.1.0-alpha01'
    implementation 'me.relex:circleindicator:2.1.4'

    implementation "org.funktionale:funktionale-either:1.2"

    //Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$rootProject.coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$rootProject.coroutinesVersion"
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    // (see https://github.com/ReactiveX/RxJava/releases for latest 3.x.x version)
    implementation 'io.reactivex.rxjava3:rxjava:3.0.0'

    //Event bus
    implementation 'com.squareup:otto:1.3.8'

    //Dependency Injection
    api "com.google.dagger:dagger:$rootProject.daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$rootProject.daggerVersion"
    api "com.google.dagger:dagger-android:$rootProject.daggerVersion"
    api "com.google.dagger:dagger-android-support:$rootProject.daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$rootProject.daggerVersion"

    //Retrofit
    implementation 'com.squareup.okhttp3:logging-interceptor:4.2.1'
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.retrofitVersion"
    implementation "com.squareup.retrofit2:converter-scalars:$rootProject.retrofitVersion"

    //Image loading
    implementation "com.github.bumptech.glide:glide:$rootProject.glideVersion"
    annotationProcessor "com.github.bumptech.glide:compiler:$rootProject.glideVersion"

    //WorkerManager
    implementation 'androidx.work:work-runtime:2.3.4'

    //JWT Token
    implementation 'io.jsonwebtoken:jjwt-api:0.11.1'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.1'
    runtimeOnly('io.jsonwebtoken:jjwt-orgjson:0.11.1') {
        exclude group: 'org.json', module: 'json'
    }
    implementation 'org.bouncycastle:bcprov-jdk15on:1.65'

    //DP3T
    implementation 'org.dpppt:dp3t-sdk-android:1.0.0'
    implementation 'com.google.android.gms:play-services-tasks:17.1.0'

    //DP3T aar dependencies
    implementation 'com.google.android.gms:play-services-base:17.3.0'
    implementation 'androidx.security:security-crypto:1.1.0-alpha01'

}

apply plugin: 'com.google.firebase.crashlytics'